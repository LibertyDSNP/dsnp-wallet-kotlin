plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
    id 'com.google.devtools.ksp' version '1.8.21-1.0.11'
}


android {
    compileSdk build_versions.compile_sdk

    defaultConfig {
        applicationId "com.unfinished.dsnp_wallet_kotlin"
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk
        versionCode 1
        versionName build_versions.versionName
        ndkVersion build_versions.ndk_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "DEEP_LINK_JUMP_TO_APP", "\"/jumpToApp\"")
        manifestPlaceholders = [deepLinkJumpToApp: "/jumpToApp"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        dev {
            storeFile file("dev.jks")
            storePassword properties.getProperty('DEV_STORE_PASSWORD')
            keyAlias properties.getProperty('DEV_KEY_ALIAS')
            keyPassword properties.getProperty('DEV_KEY_PASSWORD')
        }

        prod {
            storeFile file("prod.jks")
            storePassword properties.getProperty('PROD_STORE_PASSWORD')
            keyAlias properties.getProperty('PROD_KEY_ALIAS')
            keyPassword properties.getProperty('PROD_KEY_PASSWORD')
        }
    }

    flavorDimensions "version"
    productFlavors {
        def devSigning = signingConfigs.dev
        def prodSigning = signingConfigs.prod

        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix " dev"

            signingConfig devSigning

            buildConfigField("String", "WEB_URL", "\"dev-custodial-wallet.liberti.social\"")
            manifestPlaceholders = [webUrl: "dev-custodial-wallet.liberti.social"]
        }

        prod {
            dimension "version"

            signingConfig prodSigning

            buildConfigField("String", "WEB_URL", "\"dev-custodial-wallet.liberti.social\"")
            manifestPlaceholders = [webUrl: "dev-custodial-wallet.liberti.social"]

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = versions.kotlinCompilerExtensionVersion
    }
    namespace 'com.unfinished.dsnp_wallet_kotlin'
    lint {
        abortOnError false
        baseline file('lint-baseline.xml')
    }
}

dependencies {

    implementation project(":ccodepicker")
    implementation project(":feature_account")
    implementation project(":common")
    implementation project(':core-db')
    implementation project(':core-api')
    implementation project(':runtime')
    implementation project(':uikit')

    implementation deps.support.core_ktx
    implementation deps.support.app_compat
    implementation deps.support.material
    implementation deps.support.constraint_layout

    implementation deps.test.junit
    implementation deps.test.ext_junit
    implementation deps.test.espresso_core

    implementation deps.navigation.navigation_fragment_ktx
    implementation deps.navigation.navigation_ui_ktx

    implementation deps.extension.viewmodel_ktx
    implementation deps.extension.livedata_ktx
    implementation deps.extension.fragment_ktx

    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.compiler

    implementation deps.daggerHilt.hilt_android
    kapt deps.daggerHilt.android_compiler
    kapt deps.daggerHilt.hilt_compiler

    implementation deps.retrofit.okhttp
    implementation deps.retrofit.retrofit
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.converter_scalars
    implementation deps.retrofit.interceptor

    implementation deps.glides.glide
    annotationProcessor deps.glides.glide_compiler

    implementation deps.timber

    implementation deps.pincode_view

    implementation deps.other.coil
    implementation deps.other.coil_svg
    implementation deps.other.websocket

    implementation deps.nova.fearless_utils

    implementation deps.compose.destinations
    implementation deps.compose.viewbinding
    ksp deps.compose.ksp

    /**
     * Temp library until compose officially supports scrollbars
     */
    implementation 'com.github.nanihadesuka:LazyColumnScrollbar:1.6.3'
}